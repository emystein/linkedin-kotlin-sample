plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.6' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'org.jetbrains.kotlin.jvm' version '2.0.21' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '2.0.21' apply false
}

allprojects {
    group = 'com.example'
    version = '0.0.1'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'application'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = JavaVersion.VERSION_21.toString()
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = JavaVersion.VERSION_21.toString()
        }
    }

    sourceSets {
        main.kotlin.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += 'src/main/java'
        test.kotlin.srcDirs += 'src/test/kotlin'
        test.java.srcDirs += 'src/test/java'
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2023.0.3'
        }
    }

    dependencies {
        // Common dependencies for all subprojects
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework:spring-core'
        implementation 'com.fasterxml.jackson.core:jackson-databind'
        implementation 'com.fasterxml.jackson.core:jackson-core'

        // Feign dependencies
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'io.github.openfeign:feign-jackson'
        implementation 'io.github.openfeign:feign-slf4j'
        implementation 'io.github.openfeign.form:feign-form-spring:3.8.0'

        // Kotlin dependencies
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib'
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

        // Kotlin Logging
        implementation 'io.github.oshai:kotlin-logging-jvm:7.0.0'

        developmentOnly 'org.springframework.boot:spring-boot-devtools'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    processResources {
        filesMatching('**/*.properties') {
            expand(project.properties)
        }
    }
}
